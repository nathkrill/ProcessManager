/*
 * Copyright (c) 2016.
 *
 * This file is part of ProcessManager.
 *
 * ProcessManager is free software: you can redistribute it and/or modify it under the terms of version 3 of the
 * GNU Lesser General Public License as published by the Free Software Foundation.
 *
 * ProcessManager is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
 * the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License along with Foobar.  If not,
 * see <http://www.gnu.org/licenses/>.
 */

description = 'The overall project that manages all artefacts of the processmanager'

buildscript {
    ext.androidPluginVersion = '2.0.0'
    ext.kotlin_version = '1.0.1-2'
    ext.kotlinx_html_version = '0.5.8'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion" //1.5.0
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
    }


    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}

apply plugin: 'idea'
apply plugin: 'project-report'

ext {
    tomcatVersion = '7.0.68'
    androidCompatVersion = '23.3.0'
    androidBuildToolsVersion = '23.0.3'
    dbcpSpec = "com.zaxxer:HikariCP:2.4.0"
    collectDir = "${buildDir}/artifacts"
}

configurations {
    tomcatWars
    tomcatClasspath
}

dependencies {
    tomcatWars project(path: ":ProcessEngine:servlet", configuration: 'archives')
    tomcatWars project(path: ":PEUserMessageHandler", configuration: 'archives')
    tomcatWars project(path: ":accountmgr", configuration: 'archives')
    tomcatWars project(path: ":darwin:jvm", configuration: 'warConfig')

    tomcatClasspath 'mysql:mysql-connector-java:5.1.36'
    tomcatClasspath project(':DarwinJavaApi')
    tomcatClasspath project(':JavaCommonApi')
    tomcatClasspath project(':DarwinRealm')

}


task copyTomcatWars(type:Copy) {
    group = "dist"
    dependsOn configurations.tomcatWars
    from files { dependsOn.findAll { dep -> dep instanceof Configuration } }
    into "${collectDir}/webapps/"
}

task copyTomcatClasspath(type:Copy) {
    group = "dist"
    dependsOn configurations.tomcatClasspath
    from files { dependsOn.findAll { dep -> dep instanceof Configuration } }
    into "${collectDir}/tomcatlibs/"
}

task dist {
    group = "dist"
    dependsOn copyTomcatWars, copyTomcatClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

task run(dependsOn: [':PE-server:tomcatRun'], type:DefaultTask) {
    group='application'
    description='Run the server in a simple configuration'
}



htmlDependencyReport {
    projects = project.allprojects
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven() {
            url="http://dl.bintray.com/kotlin/kotlinx.html"
        }
    }
}

idea{
    project {
        languageLevel = '1.8'
    }
    module {
        downloadSources = true
    }
}
